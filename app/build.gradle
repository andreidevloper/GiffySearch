apply plugin: 'com.android.application'

android {
    signingConfigs {
        giffysearch {
            keyAlias 'key0'
            keyPassword obtainReleasePassword()
            storeFile file("giffysearch.jks")
            storePassword obtainReleasePassword()
        }
    }
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.as.giffysearch"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.giffysearch
        }

        releaseWithTest {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.giffysearch
        }
    }

    testBuildType obtainTestBuildType()
}

def obtainTestBuildType()
{
    def result = "debug"
    if (project.hasProperty("testBuildType"))
    {
        result = project.getProperties().get("testBuildType")
    }
    result
}

def obtainReleasePassword()
{
    def password = "*ASK_ME*"
    if (project.hasProperty("releasePassword"))
    {
        password = project.getProperties().get("releasePassword")
    }
    password
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Werror"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains:annotations-java5:15.0'

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'

    // ----------------- GLIDE ---------------
    // https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    // http://bumptech.github.io/glide/doc/configuration.html#applications
    implementation 'com.github.bumptech.glide:annotations:4.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
    // http://bumptech.github.io/glide/int/okhttp3.html
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.4.0'
    implementation('com.github.bumptech.glide:recyclerview-integration:4.4.0') {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    // ----------------- OkHttp ---------------
    // https://github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    // ----------------- Gson ---------------
    // https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // ----------------- Constraint layout -----------------
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}


